## Propose Chase Operator
# If the task is tanksoar, and the sound sensor is not silent, and there
# is no tank on radar, and energy or missies is not low, then propose the
# chase operator.
sp {propose*chase
   (state <s> ^name tanksoar
              ^io.input-link <io>
             -^missiles-energy low)
   (<io> ^sound <> silent
        -^radar.tank)
-->
   (<s> ^operator <o> +)
   (<o> ^name chase)}

sp {chase*elaborate*state*sound-direction
   (state <s> ^name chase
              ^io.input-link.sound <sound>)
-->
   (<s> ^sound-direction <sound>)}

sp {chase*elaborate*radar
   (state <s> ^name chase
              ^operator.actions <a>
              ^io.input-link.radar-status off)
-->
   (<a> ^radar.switch on
        ^radar-power.setting 13)}


## Propose Move Operator
## If the state is named chase and the sound is coming from the forward position, propose move forward.
sp {chase*propose*move
   (state <s> ^name chase
              ^sound-direction forward
              ^io.input-link.blocked.forward no)
-->
   (<s> ^operator <o> +)
   (<o> ^name move
        ^actions.move.direction forward)}

## Propose Turn Operator
## If the state is named chase and the sound is coming from left or right, turn that direction.
sp {chase*propose*turn
   (state <s> ^name chase
              ^sound-direction {<< left right >> <direction>})
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions.rotate.direction <direction>)}

## Propose Turn Operator Backward
## If the state is named chase and the sound is coming from backward, turn left.
sp {chase*propose*backward
   (state <s> ^name chase
              ^sound-direction backward)
-->
   (<s> ^operator <o> +)
   (<o> ^name turn
        ^actions.rotate.direction left)}

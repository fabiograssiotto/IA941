## Wander Operator Proposal
# If there is no tank detected on radar, and the sound is silent, and there
# is no incoming, then propose the wander operator.
sp {propose*wander
   (state <s> ^name tanksoar
              ^io.input-link <io>)
   (<io> ^sound silent
        -^radar.tank
        -^incoming.<dir> yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name wander)}

## Wander: Move Operator Proposal
# If the wander operator is selected as the superoperator, and tank is not
# blocked in the forward direction, then propose the move operator.
sp {wander*propose*move
   (state <s> ^name wander
              ^io.input-link.blocked.forward no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name move
        ^actions.move.direction forward)}

## Wander: Turn Operator Proposal
# If the wander operator is selected as the superoperator, and tank is
# blocked in the forward direction, but not on either left or right directions,
# then propose turning to that direction, turn on the radar with power setting 13.
sp {wander*propose*turn
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
   (<b> ^forward yes
        ^ { << left right >> <dir> } no)
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions <a>)
   (<a> ^rotate.direction <dir>
        ^radar.switch on
        ^radar-power.setting 13)}

## Wander: Turn Backward Operator Proposal
# If the wander operator is selected as the superoperator, and tank is
# blocked in the forward direction, left and right directions,
# then propose turning to the left.
sp {wander*propose*turn*backward
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
   (<b> ^forward yes
        ^left yes
        ^right yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name turn
        ^actions.rotate.direction left)}
